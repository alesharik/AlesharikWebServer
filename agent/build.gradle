apply plugin: 'java'
apply plugin: 'idea'
apply plugin: "com.github.johnrengelman.shadow"

buildDir = new File('./out/')

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
    }
    test {
        java {
            srcDirs 'test'
        }
    }
}

clean.doFirst {
    delete buildDir
}

shadowJar {
    baseName = project.name + '-all'
    classifier = null
    version = null
}

jar {
    manifest {
        attributes(
                'Premain-Class': 'com.alesharik.webserver.api.agent.Agent',
                'Agent-Class': 'com.alesharik.webserver.api.agent.Agent',
                'Can-Retransform-Classes': 'true',
                'Can-Redefine-Classes': 'true'
        )
    }
}

build {}

build.dependsOn test

task packageJavadoc(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
    group = 'build'

}
task packageSources(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
    group = 'build'
}

artifacts {
    archives packageJavadoc
    archives packageSources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.alesharik'
            artifactId 'alesharikwebserver-agent'
            version this.version

            from components.java
        }
    }
}

dependencies {
    compile project(':alesharikwebserver-utils')
    compile project(':alesharikwebserver-logging')

    compile group: 'io.github.lukehutch', name: 'fast-classpath-scanner', version: '3.1.15'

    compile files("${System.properties['java.home']}/../lib/tools.jar")
    testCompile project(':alesharikwebserver-test')
}